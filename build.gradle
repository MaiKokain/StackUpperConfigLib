plugins {
    id 'java'
    id 'antlr'
    id 'net.neoforged.gradle.userdev' version '7.0.181'
    id 'maven-publish'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

base.archivesName = "StackUpperConfigLibrary"

group = 'yuuria'
version = "1"

subsystems {
    parchment {
        minecraftVersion = "1.21.4"
        mappingsVersion = "2025.02.16"
    }
}

dependencies {
    implementation "net.neoforged:neoforge:21.4.97-beta"
    antlr "org.antlr:antlr4:4.13.2"
}

generateGrammarSource {
    maxHeapSize = "128m"
    arguments += ["-visitor", "-package", "yuuria.stackupper.language"]
    outputDirectory = file("${layout.getBuildDirectory().asFile.get()}/generated-src/antlr/main/yuuria/stackupper/language")
}
compileJava.dependsOn(generateGrammarSource)

jar {
    manifest {
        attributes([
                'FMLModType': 'GAMELIBRARY'
        ])
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "yuuria.stackupper"
            artifactId = "StackUpperConfigLibrary"
            version = 1

            from components.java
        }
    }
}

tasks.register("buildAndPublishToMavenLocal") {
    dependsOn Set.of("build", "publishToMavenLocal")
}